plugins {
    id "com.github.johnrengelman.shadow" version "7.1.1"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
    shadowModImplementation
    modImplementation.extendsFrom shadowModImplementation
}

repositories {
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        url = "https://maven.shedaniel.me/"
    }
    maven {
        url = "https://cursemaven.com/"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }

    shadowModImplementation("dev.itsmeow.imdlib:imdlib-fabric:${rootProject.imdlib_version}") {
        transitive = false
    }
    modImplementation("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${rootProject.cardinal_components_version}") {
        transitive = false
    }
    shadowModImplementation("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${rootProject.cardinal_components_version}") {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation("dev.emi:trinkets:${rootProject.trinkets_version}") {
        transitive = false
    }
    modRuntimeOnly("com.terraformersmc:modmenu:3.0.1") {
        transitive = false
    }
    modRuntimeOnly("curse.maven:cloth-config-319057:3559638") {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
    }
    runtimeOnly("me.shedaniel.cloth:basic-math:0.6.0")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.shadowModImplementation]
    relocate 'dev.itsmeow.imdlib', "${rootProject.maven_group}.imdlib"
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    classifier "remap"
}

task fixJar(type: Jar) {
    dependsOn remapJar
    from remapJar.archiveFile.map { zipTree(it) }
    manifest {
        from {
            zipTree(remapJar.archiveFile).find {
                it.name == "MANIFEST.MF"
            }
        }
    }
    filesMatching("**/FabricMixinPlugin.java") {
        filter {
            line -> {
                line.replaceAll("dev.itsmeow.imdlib.mixin", "dev.itsmeow.betteranimalsplus.imdlib.mixin")
            }
        }
    }
    filesMatching("**/fabric.mod.json") {
        filter {
            line -> {
                line = line.replaceAll("dev.itsmeow.imdlib", "dev.itsmeow.betteranimalsplus.imdlib")
                line = line.replaceAll('mixin.imdlib.json', 'mixin.imdlib-betteranimalsplus.json","mixin.imdlib-betteranimalsplus-fabric.json')
            }
        }
    }
    rename("mixin.imdlib.json", "mixin.imdlib-betteranimalsplus.json")
    rename("imdlib-common-refmap.json", "imdlib-betteranimalsplus-common-refmap.json")
    filesMatching("mixin.imdlib.json") {
        filter {
            line ->
                {
                    line = line.replaceAll('dev.itsmeow.imdlib.mixin', 'dev.itsmeow.betteranimalsplus.imdlib.mixin')
                    line.replaceAll('imdlib-common-refmap.json', 'imdlib-betteranimalsplus-common-refmap.json')
                }
        }
    }
    rename("mixin.imdlib-fabric.json", "mixin.imdlib-betteranimalsplus-fabric.json")
    rename("imdlib-fabric-refmap.json", "imdlib-betteranimalsplus-fabric-refmap.json")
    filesMatching("mixin.imdlib-fabric.json") {
        filter {
            line ->
                {
                    line = line.replaceAll('dev.itsmeow.imdlib.mixin', 'dev.itsmeow.betteranimalsplus.imdlib.mixin')
                    line = line.replaceAll('dev.itsmeow.imdlib.FabricMixinPlugin', 'dev.itsmeow.betteranimalsplus.imdlib.FabricMixinPlugin')
                    line.replaceAll('imdlib-fabric-refmap.json', 'imdlib-betteranimalsplus-fabric-refmap.json')
                }
        }
    }
    filesMatching("imdlib-common-refmap.json") {
        filter {
            line -> line.replaceAll('dev/itsmeow/imdlib/mixin/', 'dev/itsmeow/betteranimalsplus/imdlib/mixin/')
        }
    }
    filesMatching("imdlib-fabric-refmap.json") {
        filter {
            line -> line.replaceAll('dev/itsmeow/imdlib/mixin/', 'dev/itsmeow/betteranimalsplus/imdlib/mixin/')
        }
    }
    classifier "fabric"
}

sourcesJar.dependsOn fixJar

jar {
    classifier "dev"
    manifest {
        mainAttributes(
                "Maven-Artifact": "${rootProject.maven_group}:${rootProject.mod_id}-${project.name}:${rootProject.version}",
                "Specification-Title": rootProject.mod_id,
                "Specification-Vendor": "itsmeow",
                "Specification-Version": "1",
                "Implementation-Title": rootProject.mod_id,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "itsmeow",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Built-On-Java": "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                "Built-On": "${rootProject.mc_version}-${rootProject.fabric_api_version}"
        )
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    manifest {
        mainAttributes(
                "Maven-Artifact": "${rootProject.maven_group}:${rootProject.mod_id}-${project.name}:${rootProject.version}",
                "Specification-Title": rootProject.mod_id,
                "Specification-Vendor": "itsmeow",
                "Specification-Version": "1",
                "Implementation-Title": rootProject.mod_id,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "itsmeow",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Built-On-Java": "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                "Built-On": "${rootProject.mc_version}-${rootProject.fabric_api_version}"
        )
    }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.mod_id + "-" + project.name
            pom {
                name = rootProject.name + "-" + project.name
                url = rootProject.url
                packaging = 'jar'
                scm {
                    connection = "scm:git:git://github.com/" + rootProject.github + ".git"
                    developerConnection = "scm:git:git@github.com:" + rootProject.github + ".git"
                    url = "https://github.com/" + rootProject.github
                }
                issueManagement {
                    system = 'github'
                    url = "https://github.com/" + rootProject.github + "/issues"
                }
                organization {
                    name = 'itsmeow Mods'
                    url = 'https://itsmeow.dev/'
                }
                developers {
                    developer {
                        id = 'itsmeow'
                        name = 'itsmeowdev'
                        email = 'itsmeow@itsmeow.dev'
                    }
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def configurationNames = ["modImplementation", "modApi"]
                    configurationNames.each { configurationName ->
                        configurations[configurationName].allDependencies.each {
                            if (!(it.name in ["imdlib", "architectury-transformer", "common", "basic-math", "modmenu", "cloth-config"]) && !(it.group in ["curse.maven"])) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
            artifact(fixJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
        loom.disableDeprecatedPomGeneration(mavenFabric)
    }
}